      SUBROUTINE SNOVA(INSX,INSY) 
C
C	5-DEC-79
C	DON'T CHARGE PLAYER FOR BAD THINGS IF SUPERNOVA CAUSED BY
C	ENEMY ACTION
C
	INCLUDE 'TREKCOM/NOLIST'
      INTEGER COMDEAD 
      NSX=INSX
      NSY=INSY
C--------IF SCHEDULED SUPERNOVA (INSX=INSY=0), SELECT STAR
      IF(INSX.NE.0) GO TO 50
      NUM=RANF(0)*INSTAR+1
      DO 10 NQX=1,8 
      DO 10 NQY=1,8 
      NUM=NUM-MOD(GALAXY(NQX,NQY),10) 
      IF(NUM .LE. 0) GO TO 20 
 10   CONTINUE
C--------IF STAR IS ALREADY GONE, RETURN EMPTY-HANDED 
      RETURN
C--------IF STARSHIP IS IN THIS QUADRANT, CHOOSE STAR EXACTLY 
 20   IF(NQX.NE.QUADX .OR. NQY.NE.QUADY) GO TO 70 
C--------UNLESS STARSHIP JUST GOT HERE; THEN TREAT SUPERNOVA AS 
C        OCCURING WHILE EN ROUTE. 
      IF(JUSTIN.NE.0) GO TO 70
      NUM=RANF(0)*MOD(GALAXY(NQX,NQY),10)+1 
      DO 30 NSX=1,10
      DO 30 NSY=1,10
      IF(QUAD(NSX,NSY) .NE. IHSTAR) GO TO 30
      NUM=NUM-1 
      IF(NUM .EQ. 0) GO TO 50 
 30   CONTINUE
C--------PRINT RED ALERT (INCIPIENT SUPERNOVA) MESSAGE
 50   CALL SKIP(1)
      CALL REDALRT
      CALL CRAM(34H***INCIPIENT SUPERNOVA DETECTED AT)
      CALL CRAMLOC(2,NSX,NSY) 
      CALL CREND
      NQX=QUADX 
      NQY=QUADY 
C--------SUPERNOVA ADJACENT TO STARSHIP ENDS GAME 
      DSQ=(NSX-SECTX)**2 + (NSY-SECTY)**2 
      IF(DSQ .GT. 2.1) GO TO 80 
      CALL PROUT( 
     +   54HEMERGENCY AUTOMATIC OVERRIDE ATTEMPTS T***************,54) 
      CALL STARS
      ALLDONE=1 
      GO TO 80
C--------IF STARSHIP NOT IN SAME QUADRANT, JUST GET A MESSAGE 
 70   IF(DAMAGE(9) .NE. 0) GO TO 80 
      CALL SKIP(1)
      CALL CRAM(49HMESSAGE FROM STARFLEET COMMAND          STARDATE ) 
      CALL CRAMF(DATE,0,1)
      CALL CREND
      CALL CRAM(17H     SUPERNOVA IN) 
      CALL CRAMLOC(1,NQX,NQY) 
      CALL CRAMDMP(18H; CAUTION ADVISED.) 
C--------DESTROY ANY KLINGONS IN SUPERNOVAED QUADRANT 
 80   NUM=GALAXY(NQX,NQY) 
      KLDEAD=NUM/100
	COMDEAD=0
	ISCDEAD=0
      IF((NQX .NE. ISX) .OR. (NQY .NE. ISY)) GO TO 85 
	NSCREM=0
	ISX=0
	ISY=0
	ISATB=0
	ISCATE=0
	ISCDEAD=1
	FUTURE(6)=1E38
	FUTURE(7)=1E38
 85   IF(KLDEAD .EQ. 0) GO TO 100 
      REMKL=REMKL-KLDEAD
      IF(REMCOM .EQ. 0) GO TO 100 
      MAXLOOP=REMCOM
      DO 90 L=1,MAXLOOP 
      IF(CX(L).NE.NQX .OR. CY(L).NE.NQY) GO TO 90 
      CX(L)=CX(REMCOM)
      CY(L)=CY(REMCOM)
	CX(REMCOM)=0
	CX(REMCOM)=0
      REMCOM=REMCOM-1 
      KLDEAD=KLDEAD-1 
      COMDEAD=1 
      IF(REMCOM .EQ. 0) FUTURE(2)=1E38 
 90   CONTINUE
C--------DESTROY ROMULANS AND PLANETS IN SUPERNOVAED QUADRANT.
 100  NUM=NEWSTUF(NQX,NQY)
	NEWSTUF(NQX,NQY)=0
	NRMDEAD=NUM/10
      NROMREM=NROMREM-NRMDEAD 
      NPDEAD=NUM-NRMDEAD*10 
      IF(NPDEAD .EQ. 0) GO TO 109 
      DO 106 L=1,INPLAN 
      IF((PLNETS(L,1) .NE. NQX).OR. (PLNETS(L,2) .NE. NQY)) GO TO 106 
      DO 105 I=1,5
 105  PLNETS(L,I)=0
 106  CONTINUE
C--------DESTROY ANY BASE IN SUPERNOVAED QUADRANT 
 109  IF(REMBASE .EQ. 0) GO TO 120
      MAXLOOP=REMBASE 
      DO 110 L=1,MAXLOOP
      IF(BASEQX(L).NE.NQX .OR. BASEQY(L).NE.NQY) GO TO 110
      BASEQX(L)=BASEQX(REMBASE) 
      BASEQY(L)=BASEQY(REMBASE) 
	BASEQX(REMBASE)=0
	BASEQY(REMBASE)=0
      REMBASE=REMBASE-1 
 110  CONTINUE
C--------IF STARSHIP CAUSED SUPERNOVA, TALLY UP DESTRUCTION 
 120  IF(INSX .EQ. 0) GO TO 130 
      NUMBER=MOD(GALAXY(NQX,NQY),100) 
	KILLK=KILLK+KLDEAD
	KILLC=KILLC+COMDEAD 
	NROMKL=NROMKL+NRMDEAD
	NSCKILL=NSCKILL+ISCDEAD 
C--------IF ENEMY ACTION CAUSED SUPERNOVA, DON'T ASSESS ANY PENALTIES
	IF(IPHWHO.EQ.1)GO TO 130
	STARKL=STARKL+MOD(NUMBER,10)
	BASEKL=BASEKL+(NUMBER/10) 
	NPLANKL=NPLANKL+NPDEAD
C--------MARK SUPERNOVA IN GALAXY AND IN STAR CHART 
 130  IF(STARCH(NQX,NQY).GT.0 .AND. DAMAGE(9).NE.0) 
     +   STARCH(NQX,NQY)=1000+GALAXY(NQX,NQY) 
      IF(DAMAGE(9).EQ.0 .OR. (QUADX.EQ.NQX .AND. QUADY.EQ.NQY)) 
     +   STARCH(NQX,NQY)=1
      GALAXY(NQX,NQY)=1000
C--------IF SUPERNOVA DESTROYS LAST KLINGONS, GIVE SPECIAL MESSAGE
      IF(REMKL.NE.0 .OR. (NQX.EQ.QUADX .AND. NQY.EQ.QUADY)) GO TO 140 
      CALL SKIP(2)
      CALL PROUT(11HLUCKY YOU! ,11)    
      CALL CRAM(14HA SUPERNOVA IN)
      CALL CRAMLOC(1,NQX,NQY) 
      CALL CRAMDMP(38H HAS JUST DESTROYED THE LAST KLINGONS.) 
      CALL FINISH(1)
      RETURN
C--------IF SOME KLINGONS REMAIN, CONTINUE (OR DIE IN SUPERNOVA)
 140  IF(ALLDONE.NE.0) CALL FINISH(8) 
      RETURN
      END 
