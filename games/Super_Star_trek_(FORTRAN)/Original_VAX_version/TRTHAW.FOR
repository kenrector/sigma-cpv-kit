	SUBROUTINE THAW
C
C	5-APR-79
C	FIX BUG IN TYPEOUT ON STATE OF GAME.
C	25-APR-79
C	CLEAN UP MESSAGE ON BASES.
C	GET RID OF SPURIOUS COMMANDER ATTACK.
C	1-MAY-78
C	DON'T PRINT PASSWORD FOR 'GAME' ENTRY
C	3-MAY-78
C	USE CRAMSP TO TAKE CARE OF ALL SINGULAR/PLURAL TYPEOUTS.
C
	INCLUDE 'TREKCOM/NOLIST'
	LOGICAL*1 NAME(30)
	INTEGER DESTBAS
C
	CALL GETFN(NAME)
	IDIDIT=0
	IF(NAME(1).EQ.0) GOTO 800
	CALL CLOSE(2)
	OPEN(UNIT=2,NAME=NAME,TYPE='OLD',FORM='UNFORMATTED',
	1 ERR=800)
	READ(2,ERR=800) N,(ICOM(K),K=1,N)
	CALL CLOSE(2)
	IDIDIT=1
	GO TO 100
C*--TELL HIM WHAT KIND OF GAME HE GOT HIMSELF INTO...
	ENTRY GAME
	IDIDIT=0
100	CALL SKIP(1)
	CALL CRAM(22HYOU ARE NOW PLAYING A  )
	IF(LENGTH.EQ.1) CALL CRAM(5HSHORT)
	IF(LENGTH.EQ.2) CALL CRAM(6HMEDIUM)
	IF(LENGTH.EQ.4) CALL CRAM(4HLONG)
	IF(SKILL.EQ.1) CALL CRAM(7H NOVICE)
	IF(SKILL.EQ.2) CALL CRAM(5H FAIR)
	IF(SKILL.EQ.3) CALL CRAM(5H GOOD)
	IF(SKILL.EQ.4) CALL CRAM(7H EXPERT)
	IF(SKILL.EQ.5) CALL CRAM(9H EMERITUS)
	CALL CRAMDMP(6H GAME.)
	IF(IDIDIT.EQ.0)GO TO 110
	CALL CRAM(25HYOUR SECRET PASSWORD IS ')
	CALL CRAMS(PASSWD,8)
	CALL CRAMDMP(2H'.)
110	KILLTOT=KILLK+KILLC+NSCKILL
	CALL CRAMI(KILLTOT,0)
	CALL CRAM(4H OF )
	CALL CRAMSP(INKLING,'KLINGON')
	IF(KILLTOT.EQ.1)CALL CRAM(' HAS')
	IF(KILLTOT.NE.1)CALL CRAM(' HAVE')
	CALL CRAM(' BEEN KILLED, INCLUDING ')
	CALL CRAMSP(KILLC,'COMMANDER')
	CALL CRAMDMP('.')
	IF(SKILL.LE.2) GOTO 200
	CALL CRAM(24HTHE SUPER-COMMANDER HAS )
	IF(NSCREM.EQ.1) CALL CRAM(4HNOT )
	CALL CRAMDMP(15HBEEN DESTROYED.)
C*--GIVE HIM THE POOP ON THE BASES.
200	DESTBAS=INBASE-REMBASE
	IF(DESTBAS.EQ.0)GO TO 210
	CALL CRAMSP(DESTBAS,'BASE')
	CALL CRAM(' DESTROYED, ')
210	CALL CRAMSP(REMBASE,'BASE')
	IF(DESTBAS.NE.0)CALL CRAM(' REMAINING')
	CALL CRAMDMP('.')
C*--IS A COMMANDER CHOMPING ON A BASE?
	IF(ICSOS.EQ.0)GO TO 240
	IF(FUTURE(5).GE. 1.E38) GOTO 240
	IF(REMCOM.EQ.0 .OR. REMBASE.EQ.0)GO TO 240
	IF(MOD(GALAXY(BATX,BATY),100).LT.10)GO TO 240
	DO 220 I=1,REMCOM
	IF(CX(I).EQ.BATX .AND. CY(I).EQ.BATY)GO TO 221
220	CONTINUE
	GO TO 240
221	CALL CRAM(11HSTARBASE IN )
	CALL CRAMLOC(1,BATX,BATY)
	CALL CRAMDMP(27H IS CURRENTLY UNDER ATTACK.)
	CALL CRAM(31HIT CAN HOLD OUT UNTIL STARDATE  )
	CALL CRAMF(FUTURE(5),0,1)
	CALL CRAMDMP(1H.)
C*--IS THE SUPER COMMANDER ATTACKING?
240	IF(ISSOS.EQ.0)GO TO 245
	IF(ISATB.NE.1) GOTO 245
	CALL CRAM(11HSTARBASE IN )
	CALL CRAMLOC(1,ISX,ISY)
	CALL CRAMDMP(33H IS UNDER SUPER-COMMANDER ATTACK. )
	CALL CRAM(31HIT CAN HOLD OUT UNTIL STARDATE )
	CALL CRAMF(FUTURE(7),0,1)
	CALL CRAMDMP(1H.)
C*--ISSUE CAVEAT IF THE SUBSPACE RADIO HAS BEEN BROKEN
245	IF(ISUBDAM.EQ.0)GO TO 249
	CALL CRAM('THE SUBSPACE RADIO ')
	IF(DAMAGE(9).NE.0)CALL CRAM('IS')
	IF(DAMAGE(9).EQ.0)CALL CRAM('WAS')
	CALL CRAMDMP(' DAMAGED.  THUS YOU MAY NOT HAVE COMPLETE')
	CALL CRAMDMP('INFORMATION ON SUPERNOVAS AND ATTACKS ON BASES.')
C*--HOW MANY CASUALTIES?
249	IF(CASUAL.EQ.0) GOTO 250
	CALL CRAMSPI(CASUAL,'CASUALT','Y','IES')
	CALL CRAMDMP(' SUFFERED SO FAR.')
250	IF(NHELP.EQ.0) GOTO 260
	CALL CRAMSP(NHELP,'CALL')
	CALL CRAMDMP(' FOR HELP.')
260	CALL SKIP(1)
C*--AND LET HIM PLAY IT
	RETURN
C*--THAT WAS A BAD FREEZE OR FILE NOT OPENED
800	CALL PROUT(17H GAME NOT FROZEN.  ,17)
C*--TELL CHOOSE THAT WE DIDN'T GET ANYTHING
	PASSWD=0.D0
	RETURN
	END
